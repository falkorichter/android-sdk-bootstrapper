repositories {
    mavenCentral()
    maven {
        url "https://raw.github.com/sensorberg-dev/android-sdk/mvn-repo";
    }
}

apply plugin: 'com.android.library'
apply plugin: 'android-maven'

android {
    compileSdkVersion 20
    buildToolsVersion "20.0"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 19
    }

    lintOptions {
        abortOnError false
    }

}

dependencies {
    compile 'com.sensorberg.sdk:sensorberg-sdk:0.4.1'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

group = "com.sensorberg.sdk.bootstrapper"
artifactId = "sensorberg-sdk-bootstrapper"
version = VERSION
def tagName = "v${VERSION}"

task verifyUpload(type: Exec) {
    description "verify if this build can be archived. fails if the git tag allready exists"
    doFirst{
        exec {
            commandLine "git", "fetch", "--tags"
        }
    }
    commandLine "git", "tag"
    standardOutput = new ByteArrayOutputStream()

    doLast {
        def output = standardOutput.toString()
        output.eachLine{
            if(it.equalsIgnoreCase(tagName)){
                throw new GradleException("version ${version} has already been released as tag ${tagName}")
            }
        }
        println "version ${version} is ready to be realease as tag ${tagName}"
    }
}

task verifyReleaseBranch(type: Exec){
    description "verify if this build can be archived. fails if the git tag allready exists"

    doFirst{
        exec {
            commandLine "git", "fetch"
        }
    }

    commandLine "git", "rev-parse", "HEAD"
    standardOutput = new ByteArrayOutputStream()

    doLast {
        String currentRefName = standardOutput.toString().replaceAll("\\s","") //strip whitespaces

        def getGitRoot = "git rev-parse --show-toplevel".execute()
        if (getGitRoot.waitFor() != 0){
            throw new GradleException("could not get the root of your git project")
        }
        def gitRoot = getGitRoot.text.replaceAll("\\s","")

        String originMasterRef = file(gitRoot + "/.git/refs/remotes/origin/master").text.replaceAll("\\s","") //strip whitespaces

        if (!currentRefName.equalsIgnoreCase(originMasterRef)) {
            throw new GradleException("you are trying to release from a branch that is not origin/master. Your current revision is ${originMasterRef} but master on origin is ${currentRefName}")
        }
    }
}

uploadArchives {
    def tempDir = buildDir.path + '/github-android-sdk'
    def branch = "mvn-repo"

    doFirst{
        if (!file(tempDir).exists() ){
            println "cloning temp repo"
            exec{
                commandLine 'git', 'clone', 'git@github.com:sensorberg-dev/android-sdk.git', tempDir, '-b', branch
            }
        }
        else{
            println "updating temp repo"
            exec {
                workingDir tempDir
                commandLine "git", "reset", "HEAD", "--hard"
            }
            exec {
                workingDir tempDir
                commandLine "git", "clean", "-f"
            }
            exec {
                workingDir tempDir
                commandLine "git", "pull", "origin", branch
            }

        }
    }
    repositories {
        mavenDeployer {

            repository(url: "file://${tempDir}")

            pom.project {
                inceptionYear '2014'
                name 'Sensorberg Android SDK Bootstrapper'
                artifactId artifactId
                packaging 'aar'
                description 'Android SDK bootstrapper that helps to integrate the SDK'
                url 'https://github.com/sensorberg-dev/android-sdk'

                scm {
                    url 'https://github.com/sensorberg-dev/android-sdk-bootstrapper'
                    connection 'https://github.com/sensorberg-dev/android-sdk-bootstrapper'
                    developerConnection 'https://github.com/sensorberg-dev/android-sdk-bootstrapper'
                }
            }
        }
    }
    doLast{
        println "creating tag"
        exec {
            commandLine "git", "tag", tagName
        }
        println "pushing tag"
        exec {
            commandLine "git", "push", currentRemoteName,  tagName
        }
        exec {
            workingDir tempDir
            commandLine "git", "add", "."
        }
        exec {
            workingDir tempDir
            commandLine "git", "commit",  "-m\"Release bootstrapper ${VERSION}\""
        }
        println "pushing release"
        exec {
            workingDir tempDir
            commandLine "git", "push", "origin", branch
        }
    }
}

uploadArchives.dependsOn(verifyReleaseBranch)
uploadArchives.dependsOn(verifyUpload)
