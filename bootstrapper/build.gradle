apply plugin: 'com.android.library'
apply plugin: 'android-maven'

repositories {
    maven {
        url "https://raw.github.com/sensorberg-dev/android-sdk/mvn-repo";
    }
}

group = "com.sensorberg.sdk.bootstrapper"
archivesBaseName = 'sensorberg-sdk-bootstrapper'

if (System.getProperty("sensorberg.useGithubRepo")) {
    project.ext.repoDir = "${buildDir.path}/github-android-sdk"
    version = "${project.ext.SDK_VERSION}"
    project.ext.branch = "mvn-repo"
} else {
    project.ext.repoDir = "file://${System.getProperty("user.home")}/.m2/repository/"
    version = "${project.ext.SDK_VERSION}-SNAPSHOT"
}

if (System.getProperty("sensorberg.publishReleaseCandidate")) {
    version = "${project.ext.SDK_VERSION}-RC5"
    project.ext.branch = "mvn-repo-release-candidates"
}

android {
    compileSdkVersion 20
    buildToolsVersion "20.0"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 20
        versionName version
    }

    lintOptions {
        abortOnError false
    }
}


dependencies {
    if (version.contains("RC")){
        compile 'com.sensorberg.sdk:sensorberg-sdk:0.9.0-RC2'
    } else if  (version.contains("SNAPSHOT")) {
        compile 'com.sensorberg.sdk:sensorberg-sdk:0.9.0-SNAPSHOT'
    } else{
        compile 'com.sensorberg.sdk:sensorberg-sdk:0.9.0'
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

project.ext.tagName = "v${version}-bootstrapper"
//the name of our origin
project.ext.currentRemoteName = "origin"
project.ext.releaseRepo = "git@github.com:sensorberg-dev/android-sdk.git"



apply from: "verifyTasks.gradle"
apply from: "uploadGithub.gradle"

if (System.getProperty("sensorberg.useGithubRepo")) {
    uploadArchives.mustRunAfter(prepareGithubUpload)
    pushToGithub.mustRunAfter(uploadArchives)

    uploadArchives.dependsOn(prepareGithubUpload)
    pushToGithub.dependsOn(uploadArchives)

    prepareGithubUpload.dependsOn(verifyNoSnapshotDependencies)
    prepareGithubUpload.dependsOn(verifyReleaseBranch)
    prepareGithubUpload.dependsOn(verifyTagDoesNotExits)
}
else{
    uploadArchives.dependsOn(verifySnapshotRelease)
    uploadArchives.dependsOn(verifyTagDoesNotExits)
}

install.dependsOn(verifySnapshotRelease)
install.dependsOn(verifyTagDoesNotExits)

    uploadArchives {
        repositories {
            mavenDeployer {

                repository(url: "file://${project.ext.repoDir}")

                snapshotRepository(url: "http://172.17.12.103:8881/nexus/content/repositories/snapshots"){
                    authentication(userName: System.getProperty("sensorberg.nexusUsername"), password: System.getProperty("sensorberg.nexusPassword"))
                }

                pom.project {
                    inceptionYear '2014'
                    name 'Sensorberg Android SDK Bootstrapper'
                    artifactId project.archivesBaseName
                    groupId = project.group
                    packaging 'aar'
                    description 'Android SDK bootstrapper that helps to integrate the SDK'
                    url 'https://github.com/sensorberg-dev/android-sdk'

                    scm {
                        url 'https://github.com/sensorberg-dev/android-sdk-bootstrapper'
                        connection 'https://github.com/sensorberg-dev/android-sdk-bootstrapper'
                        developerConnection 'https://github.com/sensorberg-dev/android-sdk-bootstrapper'
                    }
                }
            }

        }
    }
